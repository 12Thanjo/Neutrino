scope {
	struct Node(data){
		this.data = data;
		this.next = null;
	};

	struct Queue(setup_arr){
		private.front = null;
		private.back = null;
		access length = 0;

		this.push = function(data){
			local new_back = new Node(data);
			if(private.length != 0){
				private.back.next = new_back;
				private.back = new_back;
			}else{
				private.front = new_back;
				private.back = new_back;
			};
			private.length += 1;
		};

		this.pop = function(){
			if(private.length != 0){
				local output = private.front;
				private.length -= 1;
				private.front = output.next;
				return output.data;
			}else{
				return null;
			};
		};

		this.peek = function(){
			if(private.front != null){
				return private.front.data;
			}else{
				return null;
			};
		};

		this.print = function(){
			local output = [];
			local target = private.front;

			while(target != null){
				output.push(target.data);
				target = target.next;
			};

			return output;
		};

		if(setup_arr != null){
			iterate setup_arr(i, value){
				this.push(value);
			};
		};
	};
	plugin.Queue = Queue;
};